struct hash_result { char* name; int token; }
%%
#
# Constant literals
#
true,      TRUE_VAL
false,     FALSE_VAL
# This is an extension to ECMAscript.
# The specification doesn't require support for an undefined literal.
undefined, UNDEF_VAL
# These are a kludge until we have real global vars.
NaN,       NAN_VAL
Infinity,  INF_VAL
#
# Keywords
#
return,  RETURN
var,     VAR
if,      IF
else,    ELSE
do,      DO
while,   WHILE
for,     FOR
switch,  SWITCH
case,    CASE
default, DEFAULT
#
# Operators
#
{,    LCURLY
},    RCURLY
(,    LPAREN
),    RPAREN
[,    LSQUARE
],    RSQUARE
.,    DOT
;,    SEMICOLON
",",  COMMA
<,    LESS_THAN
>,    GREATER_THAN
>=,   GT_EQ
<=,   LT_EQ
==,   EQ_EQ
!=,   NOT_EQ
===,  STRICT_EQ
!==,  NOT_STRICT_EQ
+,    PLUS
-,    MINUS
*,    TIMES
"%",  MOD
++,   PLUS_PLUS
--,   MINUS_MINUS
<<,   LSHIFT
>>,   RSHIFT
>>>,  URSHIFT
&,    BW_AND
|,    BW_OR
^,    XOR
!,    NOT
~,    BW_NOT
&&,   AND
||,   OR
?,    QUESTION
:,    COLON
=,    EQUALS
+=,   PLUS_EQ
-=,   MINUS_EQ
*=,   TIMES_EQ
"%=", MOD_EQ
<<=,  LSHIFT_EQ
>>=,  RSHIFT_EQ
&=,   BW_AND_EQ
|=,   BW_OR_EQ
^=,   XOR_EQ
/,    DIV
/=,   DIV_EQ
